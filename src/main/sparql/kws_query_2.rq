prefix : <urn:vocab:kws:>
prefix kws: <urn:vocab:kws:>
prefix kwsg: <urn:graph:kws:>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix text: <http://jena.apache.org/text#>

################################
# calcular scores dos caminhos #
################################

insert {graph kwsg:temp {?e kws:score2 ?score2.}}
using kwsg:temp
where {?e a rdf:Seq. (?e ?score2) text:query '%1$s'.};


########################
# selecionar resultado #
########################

drop graph kwsg:sol1;
insert {graph kwsg:sol1 {?c a rdf:Seq; kws:score ?score; ?p1 ?m. ?m ?p2 ?o.}}
using kwsg:temp
where {
  {
    select ?c (avg(coalesce(?sc,0)) as ?score)
    where {
      ?c a rdf:Seq; rdfs:member ?m.
      optional {?m kws:score ?sc}.
    }
    group By ?c
    order by desc(avg(coalesce(?sc,0)))
    limit 10
  }
  {
    {?c a rdf:Seq; rdfs:member ?m; ?p1 ?m.}
    optional {?m ?p2 ?o. filter isLiteral(?o)}
  }
};

##########################
# selecionar resultado 2 #
##########################

drop graph kwsg:sol2;
insert {graph kwsg:sol2 {?c a rdf:Seq; kws:score2 ?score2; ?p1 ?m. ?m ?p2 ?o.}}
using kwsg:temp
where {
  {
    select ?c ?score2
    where {
      ?c a rdf:Seq; kws:score2 ?score2.
    }
    order by desc(?score2)
    limit 20
  }
  {
    {?c a rdf:Seq; rdfs:member ?m; ?p1 ?m.}
    optional {?m ?p2 ?o. filter isLiteral(?o)}
  }
};

