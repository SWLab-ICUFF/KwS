prefix : <urn:vocab:kws:>
prefix kws: <urn:vocab:kws:>
prefix kwsg: <urn:graph:kws:>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix meta: <http://www.semwebtech.org/mondial/10/meta#>
prefix text: <http://jena.apache.org/text#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

insert {graph ?g {?s meta:__search_id ?id; kws:comment ?comment}}
where {
    graph ?g {
        {{?s ?p []} union {[] ?p ?s}}
        service <http://localhost:3030/Mondial/sparql> {
            optional {?s meta:__search_id ?id}
            optional {graph kwsg:indexed {?s kws:comment ?comment}}
        }
    }
};

with kwsg:groups
insert {?bag kws:size ?size.}
where {
    select ?bag (count(?m) as ?size)
    where {
        graph kwsg:groups {
            ?bag a rdf:Bag; rdfs:member ?m.
        }
    }
    group by ?bag
};

drop graph kwsg:seeds;
drop graph kwsg:groups;
drop graph kwsg:pairs;

insert {graph ?g2 {?g2 kws:answers ?g1.}}
where {
    select ?g1 ?g2 (min(?_reference) as ?reference) (count(*) as ?found)
    where {
        service <http://localhost:3030/Mondial.benchmark/sparql> {
            graph ?g1 {?s ?p ?o}
            filter (regex(str(?g1),"%1$s"))
        }
        {
            select distinct ?g2
            where {
                graph ?g2 {[] ?_p [].}
                filter (regex(str(?g2),"%1$s"))
            }
        }
        {
            select ?g1 (count(*) as ?_reference)
            where {
                service <http://localhost:3030/Mondial.benchmark/sparql> {
                    graph ?g1 {[] ?_p [].} filter (regex(str(?g1),"%1$s"))
                }
            }
            group by ?g1
        }
        filter (exists {graph ?g2 {?s ?p ?o.}})
    }
    group by ?g1 ?g2
    having (?reference = ?found)
};

insert {graph ?sol {?sol kws:score ?score; kws:size ?size.}}
where {
    graph kwsg:groups {?sol a rdf:Bag; kws:score ?score; kws:size ?size.}
};

