prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix text: <http://jena.apache.org/text#>
prefix imdb: <http://localhost:8080/resource/vocab/>
prefix rsc: <http://localhost:8080/resource/>
prefix kws: <http://localhost:8080/kws/vocab/>

insert {
  ?bag a rdf:Bag; rdfs:member ?s; rdfs:member ?member.
}
where {
  {
    select ?s (bnode() as ?bag)
    where {
      ?s ?p ?o
      filter (not exists {[a rdf:Bag; rdfs:member ?s]} && not exists {?s a rdf:Seq} && not exists {?s a rdf:Bag})
    }
    limit 1
  }
  ?s (!rdf:type | ^(!rdf:type))+ ?member.
  filter (!isLiteral(?member) && not exists {?member a rdf:Bag} && not exists {?member a rdf:Seq})
}


