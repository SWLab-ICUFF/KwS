
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix text: <http://jena.apache.org/text#>
prefix imdb: <http://localhost:8080/resource/vocab/>
prefix rsc: <http://localhost:8080/resource/>
prefix kws: <http://localhost:8080/kws/vocab/>

insert {
  ?bag a rdf:Bag.
  ?bag kws:score ?score.
  ?bag rdfs:member ?e.
  ?e ?p ?o.
}
where {
  service <http://localhost:3030/Temp/sparql> {
    {
      select ?bag (avg(?sc) as ?score)
      where {
        ?bag a rdf:Bag;
             rdfs:member [kws:score ?sc].
      }
      group By ?bag
      order by desc(avg(?sc))
      limit 10
    }
    ?bag a rdf:Bag; rdfs:member ?e.
    optional {?e ?p ?o.}
  }
}