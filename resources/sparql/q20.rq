prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix text: <http://jena.apache.org/text#>
prefix imdb: <http://localhost:8080/resource/vocab/>
prefix rsc: <http://localhost:8080/resource/>
prefix kws: <http://localhost:8080/kws/vocab/>

insert {
  ?e kws:score ?score;
     rdfs:label ?label;
     kws:comment ?comment.}
where {
  select ?e ?score ?label ?comment
  where {
    {
      select distinct ?e
      where {
        {[] rdf:_1 ?e.}
        union {[] rdf:_3 ?e.}
        union {[] rdf:_5 ?e.}
        union {[] rdf:_7 ?e.}
        union {[] rdf:_9 ?e.}
        union {[] rdf:_11 ?e.}
        union {[] rdf:_13 ?e.}
      }
    }
  	optional {
      service <http://localhost:3030/IMDb/sparql> {
      graph <urn:kws:indexed> {(?e ?score) text:query ('%1$s').}
      }
    }
    optional {
      service <http://localhost:3030/IMDb/sparql> {
        {?e rdfs:label ?label.}
      }
    }
    optional {
      service <http://localhost:3030/IMDb/sparql> {
        graph <urn:kws:indexed> {?e kws:comment ?comment.}
      }
    }
  }
}