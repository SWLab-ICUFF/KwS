
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix text: <http://jena.apache.org/text#>
prefix imdb: <http://localhost:8080/resource/vocab/>
prefix rsc: <http://localhost:8080/resource/>
prefix kws: <http://localhost:8080/kws/vocab/>

insert {
  ?seq kws:score ?score.
  ?seq a rdf:Seq.
  ?seq ?p1 ?e.
  ?e rdfs:label ?lbl.
  ?e ?p2 ?o.
}
where {
  service <http://localhost:3030/Temp/sparql> {
    {
      select ?seq (avg(?sc) as ?score)
      where {
        ?seq a rdf:Seq;
             ?p [kws:score ?sc].
      }
      group By ?seq
      order by desc(sum(?sc))
      limit 20
    }
    ?seq a rdf:Seq;
         ?p1 ?e.
    optional {?e ?p2 ?o.}
  }
  service <http://localhost:3030/IMDb/sparql> {
    optional {?e rdfs:label ?lbl.}
    optional {graph <> {?e kws:comment ?comm.}}
  }
}